<!DOCTYPE html>
<html lang="en"><head><meta charset="UTF-8"/><meta name="viewport" content="width=device-width, initial-scale=1.0"/><title>Examples · CoherentStructures.jl</title><link href="https://cdnjs.cloudflare.com/ajax/libs/normalize/4.2.0/normalize.min.css" rel="stylesheet" type="text/css"/><link href="https://fonts.googleapis.com/css?family=Lato|Roboto+Mono" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/font-awesome/4.6.3/css/font-awesome.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/9.12.0/styles/default.min.css" rel="stylesheet" type="text/css"/><script>documenterBaseURL="."</script><script src="https://cdnjs.cloudflare.com/ajax/libs/require.js/2.2.0/require.min.js" data-main="assets/documenter.js"></script><script src="siteinfo.js"></script><script src="../versions.js"></script><link href="assets/documenter.css" rel="stylesheet" type="text/css"/></head><body><nav class="toc"><h1>CoherentStructures.jl</h1><select id="version-selector" onChange="window.location.href=this.value" style="visibility: hidden"></select><form class="search" id="search-form" action="search.html"><input id="search-query" name="q" type="text" placeholder="Search docs"/></form><ul><li><a class="toctext" href="index.html">Home</a></li><li class="current"><a class="toctext" href="examples.html">Examples</a><ul class="internal"><li><a class="toctext" href="#Rotating-Double-Gyre-1">Rotating Double Gyre</a></li><li><a class="toctext" href="#Bickley-Jet-1">Bickley Jet</a></li><li><a class="toctext" href="#Geostrophic-Ocean-Flow-1">Geostrophic Ocean Flow</a></li><li><a class="toctext" href="#Standard-Map-1">Standard Map</a></li></ul></li><li><a class="toctext" href="basics.html">Basics</a></li><li><span class="toctext">Methods</span><ul><li><a class="toctext" href="fem.html">FEM-based methods</a></li><li><a class="toctext" href="elliptic.html">Geodesic vortices</a></li><li><a class="toctext" href="Laplace.html">Graph Laplacian-based methods</a></li></ul></li></ul></nav><article id="docs"><header><nav><ul><li><a href="examples.html">Examples</a></li></ul><a class="edit-page" href="https://github.com/CoherentStructures/CoherentStructures.jl/blob/master/docs/src/examples.md"><span class="fa"></span> Edit on GitHub</a></nav><hr/><div id="topbar"><span>Examples</span><a class="fa fa-bars" href="#"></a></div></header><h1><a class="nav-anchor" id="Examples-1" href="#Examples-1">Examples</a></h1><p>As a quick hands-on introduction, we demonstrate the usage of the CoherentStructures package on some classic flow problems. For references to the original works in which the methods were developed see the respective help page.</p><h2><a class="nav-anchor" id="Rotating-Double-Gyre-1" href="#Rotating-Double-Gyre-1">Rotating Double Gyre</a></h2><h3><a class="nav-anchor" id="FEM-Based-Methods-1" href="#FEM-Based-Methods-1">FEM-Based Methods</a></h3><p>The following code-snippet shows how these methods can be used.</p><pre><code class="language-">using CoherentStructures,Arpack
LL = [0.0,0.0]; UR = [1.0,1.0];
ctx = regularTriangularGrid((50,50),LL,UR)

A = x-&gt; mean_diff_tensor(rot_double_gyre,x,[0.0,1.0], 1.e-10,tolerance= 1.e-4)
K = assembleStiffnessMatrix(ctx,A)
M = assembleMassMatrix(ctx)
λ, v = eigs(-K,M,which=:SM);</code></pre><p>This velocity field is given by the <code>rot_double_gyre</code> function. The second argument to <code>mean_diff_tensor</code> are the times at which we average the pullback diffusion tensors. The third parameter is the step size δ used for the finite-difference scheme, <code>tolerance</code> is passed to the ODE solver from <a href="http://juliadiffeq.org/">DifferentialEquations.jl</a>. In the above, <code>A(x)</code> approximates the mean diffusion tensor given by</p><div>\[A(x) = \sum_{t \in \mathcal T}(D\Phi^t(x))^{-1} (D\Phi^t x)^{-T}\]</div><p>The eigenfunctions saved in <code>v</code> approximate those of <span>$\Delta^{dyn}$</span></p><pre><code class="language-">import Plots
res = [plot_u(ctx, v[:,i],colorbar=:none,clim=(-3,3)) for i in 1:6];
Plots.plot(res...,margin=-10Plots.px)</code></pre><p>Looking at the spectrum, there appears a gap after the third eigenvalue:</p><pre><code class="language-">Plots.scatter(1:6, real.(λ))</code></pre><p>We can use the <a href="https://github.com/JuliaStats/Clustering.jl">Clustering.jl</a> package to compute coherent structures from the first two nontrivial eigenfunctions:</p><pre><code class="language-">using Clustering
numclusters=2
res = kmeans(permutedims(v[:,2:numclusters+1]),numclusters+1)
u = kmeansresult2LCS(res)
Plots.plot([plot_u(ctx,u[:,i],200,200,color=:viridis) for i in [1,2,3]]...)
</code></pre><h3><a class="nav-anchor" id="Geodesic-vortices-1" href="#Geodesic-vortices-1">Geodesic vortices</a></h3><p>The rotating double gyre model was introduced by <a href="https://dx.doi.org/10.1137/100794110">Mosovsky &amp; Meiss</a>. It can be derived from the stream function $ \psi(x,y,t)=(1−s(t))\psi<em>P +s(t)\psi</em>F$ <span>$\psi_P (x, y) = \sin(2\pi x) \sin(\pi y)$</span> <span>$\psi_F (x, y) = \sin(\pi x) \sin(2\pi y)$</span></p><p>where <span>$s$</span> is (usually taken to be) a cubic interpolating function satisfying <span>$s(0) = 0$</span> and <span>$s(1) = 1$</span>. It therefore interpolates two double gyre flow fields, from horizontally to vertically arranged counter-rotating gyres. The corresponding velocity field is provided by the package and callable as <code>rot_double_gyre</code>.</p><p>Here, we demonstrate how to calculate black-hole vortices, see <a href="elliptic.html#Geodesic-elliptic-material-vortices-1">Geodesic elliptic material vortices</a> for references and details.</p><pre><code class="language-">using CoherentStructures
import Tensors, OrdinaryDiffEq, Plots

const q = 51
const tspan = collect(range(0.,stop=1.,length=q))
ny = 101
nx = 101
N = nx*ny
xmin, xmax, ymin, ymax = 0.0, 1.0, 0.0, 1.0
xspan, yspan = range(xmin,stop=xmax,length=nx), range(ymin,stop=ymax,length=ny)
P = vcat.(xspan&#39;,yspan)
const δ = 1.e-6
mCG_tensor = u -&gt; CG_tensor(rot_double_gyre,u,tspan,δ,tolerance=1e-6,solver=OrdinaryDiffEq.Tsit5())

C = map(mCG_tensor,P)

LCSparams = (.1, 0.5, 0.01, 0.2, 0.3, 60)
vals, signs, orbits = ellipticLCS(C,xspan,yspan,LCSparams);</code></pre><p>The results are then visualized as follows.</p><pre><code class="language-">using Statistics
λ₁, λ₂, ξ₁, ξ₂, traceT, detT = tensor_invariants(C)
# damp &quot;outliers&quot;
l₁ = min.(λ₁,quantile(λ₁[:],0.999))
l₁ = max.(λ₁,1e-2)
l₂ = min.(λ₂,quantile(λ₂[:],0.995))

fig = Plots.heatmap(xspan,yspan,log10.(l₂),aspect_ratio=1,color=:viridis,
            title=&quot;FTLE-field and transport barriers&quot;,xlims=(xmin, xmax),ylims=(ymin, ymax),leg=true)
for i in eachindex(orbits)
    Plots.plot!(orbits[i][1,:],orbits[i][2,:],w=3,label=&quot;T = $(round(vals[i],digits=2))&quot;)
end
Plots.plot(fig)</code></pre><h2><a class="nav-anchor" id="Bickley-Jet-1" href="#Bickley-Jet-1">Bickley Jet</a></h2><p>The Bickley jet flow is a kinematic idealized model of a meandering zonal jet flanked above and below by counterrotating vortices. It was introduced by <a href="https://dx.doi.org/10.1175/JAS4036.1">Rypina et al.</a>; cf. also <a href="https://doi.org/10.1063/1.858639">del‐Castillo‐Negrete and Morrison</a>.</p><p>The Bickley jet is described by a time-dependent velocity field arising from a stream-function. The corresponding velocity field is provided by the package and callable as <code>bickleyJet</code>.</p><h3><a class="nav-anchor" id="Geodesic-vortices-2" href="#Geodesic-vortices-2">Geodesic vortices</a></h3><p>Here we briefly demonstrate how to find material barriers to diffusive transport; see <a href="elliptic.html#Geodesic-elliptic-material-vortices-1">Geodesic elliptic material vortices</a> for references and details.</p><pre><code class="language-">using CoherentStructures, Arpack
import Tensors, OrdinaryDiffEq

const q = 81
const tspan = collect(range(0.,stop=3456000.,length=q))
ny = 120
nx = div(ny*24,6)
N = nx*ny
xmin, xmax, ymin, ymax = 0.0 - 2.0, 6.371π + 2.0, -3.0, 3.0
xspan, yspan = range(xmin,stop=xmax,length=nx), range(ymin,stop=ymax,length=ny)
P = vcat.(xspan&#39;,yspan)
const δ = 1.e-6
const DiffTensor = Tensors.SymmetricTensor{2,2}([2., 0., 1/2])
mCG_tensor = u -&gt; av_weighted_CG_tensor(bickleyJet,u,tspan,δ,
          D=DiffTensor,tolerance=1e-6,solver=OrdinaryDiffEq.Tsit5())

C̅ = map(mCG_tensor,P)

LCSparams = (.1, 0.5, 0.04, 0.5, 1.8, 60)
vals, signs, orbits = ellipticLCS(C̅,xspan,yspan,LCSparams);</code></pre><p>The result is visualized as follows:</p><pre><code class="language-">import Plots
using Statistics
λ₁, λ₂, ξ₁, ξ₂, traceT, detT = tensor_invariants(C̅)
l₁ = min.(λ₁,quantile(λ₁[:],0.999))
l₁ = max.(λ₁,1e-2)
l₂ = min.(λ₂,quantile(λ₂[:],0.995))
begin
    fig = Plots.heatmap(xspan,yspan,log10.(l₁.+l₂),aspect_ratio=1,color=:viridis,
            title=&quot;DBS-field and transport barriers&quot;,xlims=(0., 6.371π),ylims=(-3., 3.),leg=true)
    for i in eachindex(orbits)
        Plots.plot!(orbits[i][1,:],orbits[i][2,:],w=3,label=&quot;T = $(round(vals[i],digits=2))&quot;)
    end
end
Plots.plot(fig)</code></pre><h3><a class="nav-anchor" id="FEM-based-Methods-1" href="#FEM-based-Methods-1">FEM-based Methods</a></h3><p>The Bickley Jet is described by a time-dependent velocity field arising from a stream-function. Instead of using the <code>bickleyJet</code> function to get this velocity field, we can use the <code>@velo_from_stream</code> macro:</p><div><pre><code class="language-julia">using CoherentStructures
# after this, &#39;bickley&#39; will reference a Dictionary of functions
# access it via the desired signature. e.g. F = bickley[:(dU, U, p, t)]
# for the right side of the equation of variation.
bickley = @velo_from_stream stream begin
    stream = psi₀ + psi₁
    psi₀   = - U₀ * L₀ * tanh(y / L₀)
    psi₁   =   U₀ * L₀ * sech(y / L₀)^2 * re_sum_term

    re_sum_term =  Σ₁ + Σ₂ + Σ₃

    Σ₁  =  ε₁ * cos(k₁*(x - c₁*t))
    Σ₂  =  ε₂ * cos(k₂*(x - c₂*t))
    Σ₃  =  ε₃ * cos(k₃*(x - c₃*t))

    k₁ = 2/r₀      ; k₂ = 4/r₀    ; k₃ = 6/r₀

    ε₁ = 0.0075    ; ε₂ = 0.15    ; ε₃ = 0.3
    c₂ = 0.205U₀   ; c₃ = 0.461U₀ ; c₁ = c₃ + (√5-1)*(c₂-c₃)

    U₀ = 62.66e-6  ; L₀ = 1770e-3 ; r₀ = 6371e-3
end;</code></pre></div><p>As we are using a periodic domain in one direction:</p><div><pre><code class="language-julia">LL = [0.0,-3.0]; UR=[6.371π,3.0]
ctx = regularTriangularGrid((100,30),LL,UR,quadrature_order=1)
predicate = (p1,p2) -&gt; abs(p1[2] - p2[2]) &lt; 1e-10 &amp;&amp; peuclidean(p1[1], p2[1], 6.371π) &lt; 1e-10
bdata = CoherentStructures.boundaryData(ctx,predicate,[]);</code></pre></div><p>Using a FEM-based method to compute coherent structures:</p><pre><code class="language-">using Arpack,Statistics
cgfun = (x -&gt; mean(pullback_diffusion_tensor(bickley, x,range(0.0,stop=40*3600*24,length=81),
     1.e-8,tolerance=1.e-5)))

K = assembleStiffnessMatrix(ctx,cgfun,bdata=bdata)
M = assembleMassMatrix(ctx,bdata=bdata)
λ, v = eigs(K,M,which=:SM, nev= 10)
plot_real_spectrum(λ)</code></pre><p>K-means clustering gives something we can plot:</p><pre><code class="language-">using Clustering,Plots
n_partition = 8
res = kmeans(permutedims(v[:,2:n_partition]),n_partition)
u = kmeansresult2LCS(res)
u_combined = sum([u[:,i]*i for i in 1:n_partition])
plot_u(ctx, u_combined,200,200,bdata=bdata,
    color=:rainbow,colorbar=:none,title=&quot;$n_partition-partition of Bickley Jet&quot;)</code></pre><p>We ran <code>kmeans</code> only once. To get better results, <code>kmeans</code> should be run several times and only the run with the lowest objective function be kept.</p><h2><a class="nav-anchor" id="Geostrophic-Ocean-Flow-1" href="#Geostrophic-Ocean-Flow-1">Geostrophic Ocean Flow</a></h2><p>For a more realistic application, we consider an unsteady ocean surface velocity data set obtained from satellite altimetry measurements produced by SSALTO/DUACS and distributed by AVISO. The particular space-time window has been used several times in the literature.</p><h3><a class="nav-anchor" id="Geodesic-vortices-3" href="#Geodesic-vortices-3">Geodesic vortices</a></h3><p>Here, we demonstrate how to detect material barriers to diffusive transport.</p><pre><code class="language-">using CoherentStructures
import JLD2, OrdinaryDiffEq, Plots
###################### load and interpolate velocity data sets #############
JLD2.@load(&quot;../../examples/Ocean_geostrophic_velocity.jld2&quot;)

UI, VI = interpolateVF(Lon,Lat,Time,UT,VT)
p = (UI,VI)

############################ integration set up ################################
q = 91
t_initial = minimum(Time)
t_final = t_initial + 90
const tspan = range(t_initial,stop=t_final,length=q)
nx = 500
ny = Int(floor(0.6*nx))
N = nx*ny
xmin, xmax, ymin, ymax = -4.0, 6.0, -34.0, -28.0
xspan, yspan = range(xmin,stop=xmax,length=nx), range(ymin,stop=ymax,length=ny)
P = vcat.(xspan&#39;,yspan)
const δ = 1.e-5
mCG_tensor = u -&gt; av_weighted_CG_tensor(interp_rhs,u,tspan,δ,p = p,tolerance=1e-6,solver=OrdinaryDiffEq.Tsit5())

C̅ = map(mCG_tensor,P)
LCSparams = (.09, 0.5, 0.05, 0.5, 1.0, 60)
vals, signs, orbits = ellipticLCS(C̅,xspan,yspan,LCSparams);</code></pre><p>The result is visualized as follows:</p><pre><code class="language-">using Statistics
λ₁, λ₂, ξ₁, ξ₂, traceT, detT = tensor_invariants(C̅)
l₁ = min.(λ₁,quantile(λ₁[:],0.999))
l₁ = max.(λ₁,1e-2)
l₂ = min.(λ₂,quantile(λ₂[:],0.995))
fig = Plots.heatmap(xspan,yspan,log10.(l₁.+l₂),aspect_ratio=1,color=:viridis,
            title=&quot;DBS-field and transport barriers&quot;,xlims=(xmin, xmax),ylims=(ymin, ymax),leg=true)
for i in eachindex(orbits)
    Plots.plot!(orbits[i][1,:],orbits[i][2,:],w=3,label=&quot;T = $(round(vals[i],digits=2))&quot;)
end
Plots.plot(fig)</code></pre><h2><a class="nav-anchor" id="Standard-Map-1" href="#Standard-Map-1">Standard Map</a></h2><p>The &quot;standard map&quot; with parameter <span>$a$</span> is defined on a 2-dimensional doubly 2π-periodic domain by <span>$f(x,y) = (x+ y+ a \sin(x),y + a\cos(x))$</span>.</p><p>For <span>$a = 0.971635$</span>, the standard map is implemented in <code>CoherentStructures.standardMap</code>, its Jacobi-matrix in <code>CoherentStructures.DstandardMap</code>.</p><p>See also <a href="https://arxiv.org/abs/1505.05056">Froyland &amp; Junge (2015)</a>, who calculate Coherent Structures for this map.</p><p>Below are some orbits of the standard map</p><pre><code class="language-">using Random
to_plot = []
for i in 1:50
    Random.seed!(i)
    x = rand(2)*2π
    for i in 1:500
        x = CoherentStructures.standardMap(x)
        push!(to_plot,x)
    end
end
Plots.scatter([x[1] for x in to_plot],[x[2] for x in to_plot],
    m=:pixel,ms=1,aspect_ratio=1,legend=:none)</code></pre><p>Approximating the Dynamical Laplacian by FEM methods is straightforward:</p><pre><code class="language-">using CoherentStructures #hide
using Tensors, Plots, Arpack, Printf
ctx = regularTriangularGrid((100,100), [0.0,0.0],[2π,2π])
pred  = (x,y) -&gt; (peuclidean(x[1],y[1],2π) &lt; 1e-9 &amp;&amp; peuclidean(x[2],y[2],2π) &lt; 1e-9
bdata = boundaryData(ctx,pred) #Periodic boundary

id2 = one(Tensors.Tensor{2,2}) # 2D identity tensor
cgfun = x -&gt; 0.5*(id2 +  Tensors.dott(Tensors.inv(CoherentStructures.DstandardMap(x))))

K = assembleStiffnessMatrix(ctx,cgfun,bdata=bdata)
M = assembleMassMatrix(ctx,lumped=false,bdata=bdata)
@time λ, v = eigs(-1*K,M,which=:SM)
Plots.plot(
    [plot_u(ctx,v[:,i],bdata=bdata,title=@sprintf(&quot;\\lambda = %.3f&quot;,λ[i]),
        clim=(-0.25,0.25),colorbar=:none)
         for i in 1:6]...)</code></pre><footer><hr/><a class="previous" href="index.html"><span class="direction">Previous</span><span class="title">Home</span></a><a class="next" href="basics.html"><span class="direction">Next</span><span class="title">Basics</span></a></footer></article></body></html>
